
<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <title>Notes of Write Scheme 48 Hours</title>
    <meta name="description" content="这是一份学习笔记，《48 Hours》通过开发一个 Scheme 解释器介绍了 Haskell 语言">
    <meta name="author" content="刘鑫">

    <!-- Le HTML5 shim, for IE6-8 support of HTML elements -->
    <!--[if lt IE 9]>
      <script src="http://html5shim.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Le styles -->
    <link href="/assets/themes/twitter/bootstrap/css/bootstrap.min.css" rel="stylesheet">
    <link href="/assets/themes/twitter/css/style.css?body=1" rel="stylesheet" type="text/css" media="all">

    <!-- Le fav and touch icons -->
  <!-- Update these with your own images
    <link rel="shortcut icon" href="images/favicon.ico">
    <link rel="apple-touch-icon" href="images/apple-touch-icon.png">
    <link rel="apple-touch-icon" sizes="72x72" href="images/apple-touch-icon-72x72.png">
    <link rel="apple-touch-icon" sizes="114x114" href="images/apple-touch-icon-114x114.png">
  -->
  </head>

  <body>

    <div class="navbar">
      <div class="navbar-inner">
        <div class="container">
          <a class="brand" href="/">挖坑不填兽</a>
          <ul class="nav">
            
            
            


  
    
      
      	
      	<li><a href="/archive.html">Archive</a></li>
      	
      
    
  
    
      
    
  
    
      
      	
      	<li><a href="/categories.html">Categories</a></li>
      	
      
    
  
    
      
    
  
    
      
      	
      	<li><a href="/pages.html">Pages</a></li>
      	
      
    
  
    
      
    
  
    
      
      	
      	<li><a href="/tags.html">Tags</a></li>
      	
      
    
  



          </ul>
        </div>
      </div>
    </div>

    <div class="container">

      <div class="content">
        
<div class="page-header">
  <h1>Notes of Write Scheme 48 Hours <small>Supporting tagline</small></h1>
</div>

<div class="row">
  <div class="span8">
    
<h2 id="section">原子词素解析器</h2>

<pre class="language-haskell"><code>
parseAtom :: Parser LispVal
parseAtom = do first &lt;- letter &lt;|&gt; symbol -- 字母或操作符开头
               rest &lt;- many (letter &lt;|&gt; digit &lt;|&gt; symbol) -- 一到多个
               let atom = [first] ++ rest -- 迭加
               return $ case atom of -- 根据词素解析生成返回的解析值
                          "#t" -&gt; Bool True 
                          "#f" -&gt; Bool False
                          otherwise -&gt; Atom atom

</code></pre>

<h3 id="section-1">练习 2.3.2</h3>
<p>(Number . read) 如果“.”两边没有空格会出错，应该是错把 Number.read 当 作一个类型了。
看了这个才知道我对 Monad 还是玩的不熟，意识不够：</p>

<pre class="language-haskell"><code>
parseNumber :: Parser LispVal
parseNumber = many1 digit &gt;&gt;= (return . Number . read)

</code></pre>

<h3 id="section-2">练习 2.3.3</h3>

<p>这个逃逸字符处理的思路我想到了，但是还是没有意识到 do 模式下 many 和 return 可以这样简洁漂亮的组合：</p>

<pre class="language-haskell"><code>escapedChars :: Parser Char
escapedChars = do char '\\' 
                  x &lt;- oneOf "\\\"nrt" 
                  return $ case x of 
                    '\\' -&gt; x
                    '"'  -&gt; x
                    'n'  -&gt; '\n'
                    'r'  -&gt; '\r'
                    't'  -&gt; '\t'
</code></pre>

<p>附扩展后的 parseString ，这个倒没大变化：</p>

<pre class="language-haskell"><code>parseString :: Parser LispVal
parseString = do char '"'
                 x &lt;- many $ escapedChars &lt;|&gt; noneOf "\"\\"
                 char '"'
                 return $ String x
</code></pre>

<h3 id="section-3">练习 2.3.4</h3>

<p>基本上进制不同的解析是一个力气活。我原本想用 do return case 的方式，但 是后来看还是答案中的每种进制分别解析比较合理，因为逻辑上要区分一些特殊情况。</p>

<p>这其中需要注意的知识点就是 Numeric 模块中的 readXXX 函数都是返回二元组， 要像如下代码这样取第一个元素出来才是我们需要的答案。</p>

<pre class="language-haskell"><code>oct2dig x = fst $ readOct x !! 0
hex2dig x = fst $ readHex x !! 0
bin2dig  = bin2dig' 0
bin2dig' digint "" = digint
bin2dig' digint (x:xs) = let old = 2 * digint + (if x == '0' then 0 else 1) in
                         bin2dig' old xs
</code></pre>

<h3 id="section-4">练习 2.3.5</h3>
<p>这里在函数内部嵌套 do 的技巧值得学习</p>

<pre class="language-haskell"><code>parseCharacter :: Parser LispVal
parseCharacter = do
  try $ string "#\\"
  value &lt;- try (string "newline" &lt;|&gt; string "space")
           &lt;|&gt; do {x &lt;- anyChar; notFollowedBy alphaNum; return [x]}
  return $ Character $ case value of
                         "space" -&gt; ' '
                         "newline" -&gt; '\n'
                         otherwise -&gt; (value !! 0)
</code></pre>

<p>这里开始更多的使用 try ，这个函数我还不熟悉。</p>

<pre><code>parseExpr :: Parser LispVal
parseExpr = parseAtom
            &lt;|&gt; parseString
            &lt;|&gt; try parseNumber
            &lt;|&gt; try parseBool
            &lt;|&gt; try parseCharacter
</code></pre>

<h3 id="section-5">练习 2.3.7</h3>
<p>原文这里似乎有错，我最终用的是 parseNumber 而不是 parseDecimal，这是考 虑到应该允许输入不同进制的复数。</p>

<pre><code>parseComplex :: Parser LispVal
parseComplex = do x &lt;- (try parseFloat &lt;|&gt; parseDecimal)
                  char '+' 
                  y &lt;- (try parseFloat &lt;|&gt; parseDecimal)
                  char 'i' 
                  return $ Complex (toDouble x :+ toDouble y)
</code></pre>

<h2 id="recursive-parsers">3.4 Recursive Parsers</h2>
<p>CLOSED: 2010-11-23 二 01:00
递归解释器这里令人赞叹的利用了 monad then ： CLOSED: 2010-11-23 二 01:00</p>

<pre class="language-haskell"><code>parseDottedList :: Parser LispVal
parseDottedList = do
    head &lt;- endBy parseExpr spaces
    tail &lt;- char '.' &gt;&gt; spaces &gt;&gt; parseExpr
    return $ DottedList head tail
</code></pre>

<p>有前趋状态（如某些数据会引用前缀，类似数组则会迭加元素）的时候，需要用 try 。可以确保出错时回溯到出错前的状态。</p>

<p>在加入了练习补充内容的代码中增加 list 解析过程时，会有编译错误，需要 把 list 的解析器单独取出来。</p>

<pre class="language-haskell"><code>parseLst :: Parser LispVal
parseLst = do char '('
              x &lt;- (try parseList) &lt;|&gt; parseDottedList
              char ')'
              return x
</code></pre>
<p>然后 parseExpr 就成为这样：</p>

<pre class="language-haskell"><code>parseExpr :: Parser LispVal
parseExpr = parseAtom
            &lt;|&gt; parseString
            &lt;|&gt; try parseNumber
            &lt;|&gt; try parseRatio
            &lt;|&gt; try parseFloat
            &lt;|&gt; try parseComplex
            &lt;|&gt; try parseBool
            &lt;|&gt; try parseCharacter
            &lt;|&gt; parseQuoted
            &lt;|&gt; parseLst
</code></pre>

<p>在本节的几个解析器实现中，递归调用 parseExpr ，实现了整个解析器对递归语 法结构的解析。</p>

<h3 id="section-6">练习 2.4.3</h3>

<p>在这里 wikibooks 给出了两组不同的实现，一个是实现一种 AnyList。此实现有 一些错误，首先是没有给出 Nil 的实现，我自己尝试做了一个：</p>

<pre class="language-haskell"><code>data LispVal = Atom String
             | List [LispVal]
             | DottedList [LispVal] LispVal
             | Number Integer
             | Float Double
             | String String
             | Bool Bool
             | Character Char
             | Ratio Rational
             | Complex (Complex Double)
             | Vector (Array Int LispVal)
             | Nil ()
</code></pre>

<p>然后 optionalSpaces 也不存在，实际上应该就是 spaces。</p>

<pre class="language-haskell"><code>parseAnyList :: Parser LispVal
parseAnyList = do
  char '('
  spaces
  head &lt;- sepEndBy parseExpr spaces
  tail &lt;- (char '.' &gt;&gt; spaces &gt;&gt; parseExpr) &lt;|&gt; return (Nil ())
  spaces
  char ')'
  return $case tail of
            (Nil ()) -&gt; List head
            otherwise -&gt; DottedList head tail
</code></pre>

<p>该页上还给出一种方法是扩展 parseList ，此方法不使用 Nil。</p>

<pre class="language-haskell"><code>-- parseList' :: Parser LispVal
parseList' head = do char '.' &gt;&gt; spaces1
                     tail &lt;- parseExpr
                     spaces &gt;&gt; char ')'
                     return $ DottedList head tail

parseList :: Parser LispVal
parseList = do char '(' &gt;&gt; spaces
               head &lt;- parseExpr `sepEndBy` spaces1
               (parseList' head)&lt;|&gt; (spaces &gt;&gt; char ')' &gt;&gt; (return $ List head))
</code></pre>

<table>
  <tbody>
    <tr>
      <td>此例中仍然遇到在有 choice 运算符（&lt;</td>
      <td>&gt;） 的情况下无法嵌套 do，于是定义了一个辅助函数。</td>
    </tr>
  </tbody>
</table>

<h2 id="evalution-part-1">Evalution, Part 1</h2>

<h3 id="beginnings-of-an-evaluator-primitives">4.2 Beginnings of an evaluator: Primitives</h3>
<p>因为 Lisp 族系代码即数据，所以 eval LispVal 变量就是返回 LispVal本身。</p>

<p>这里使用 @ 运算符，绑定进行局部匹配的变量：</p>

<pre class="language-haskell"><code>eval :: LispVal -&gt; LispVal
eval val@(String _) = val
eval val@(Number _) = val
eval val@(Bool _) = val
eval (List [Atom "quote", val]) = val
</code></pre>

<p>加入 eval 后的主函数再次体现了 monad 绑定与 . 运算符的组合技巧。</p>

<pre class="language-haskell"><code>main :: IO ()
main = getArgs &gt;&gt;= putStrLn . show . eval . readExpr . (!! 0)
</code></pre>

<p>这个函数中的 ($args) 巧妙使用了 $ 运算符的右结合</p>

<pre class="language-haskell"><code>apply :: String -&gt; [LispVal] -&gt; LispVal
apply func args = maybe (Bool False) ($ args) $ lookup func primitives
</code></pre>

<h2 id="intermezzo-error-checking--exceptions">Intermezzo: Error Checking &amp; Exceptions</h2>

<p>错误处理一章中展示了 Either 的运用。包括 throwError 和 catchError。</p>

<p>在我们的项目中，用 trapError 方法将 catchError 包装起来。</p>

<pre class="language-haskell"><code>trapError action = catchError action (return . show)
</code></pre>

<p>对于正确的执行结果，我们用 extractValue 析出结果值：</p>

<pre class="language-haskell"><code>extractValue :: ThrowsError a -&gt; a
extractValue (Right val) = val
</code></pre>

<p>eval 中要多处理出错的情况</p>

<pre class="language-haskell"><code>eval :: LispVal -&gt; ThrowsError LispVal
eval val@(String _) = return val
eval val@(Number _) = return val
eval val@(Bool _) = return val
eval (List [Atom "quote", val]) = return val
eval (List (Atom func : args)) = mapM eval args &gt;&gt;= apply func
eval badForm = throwError $ BadSpecialForm "Unrecognized special form" badForm
</code></pre>

<p>函数 primitives 只需要修改返回值声明</p>

<pre class="language-haskell"><code>primitives :: [(String, [LispVal] -&gt; ThrowsError LispVal)]
</code></pre>

<p>…</p>

<p>因为我们不需要修改它的每一种实现，只要修改 unaryOp 和 numericBinop 函数即可：</p>

<pre class="language-haskell"><code>numericBinop :: (Integer -&gt; Integer -&gt; Integer) -&gt; [LispVal] -&gt; ThrowsError LispVal
numericBinop op singleVal@[_] = throwError $ NumArgs 2 singleVal
numericBinop op params = mapM unpackNum params &gt;&gt;= return . Number . foldl1 op 

unaryOp :: (LispVal -&gt; LispVal) -&gt; [LispVal] -&gt; ThrowsError LispVal
unaryOp f [v] = return $ f v
</code></pre>

<p>unpackNum 中，我们对无效值作了异常抛出。如果想要严格类型约束，可以将针对 String 和 List 的实现注释掉。</p>

<pre class="language-haskell"><code>unpackNum :: LispVal -&gt; ThrowsError Integer
unpackNum (Number n) = return n
unpackNum (String n) = let parsed = reads n in
                       if null parsed
                          then throwError $ TypeMismatch "number" $ String n
                          else return $ fst ( parsed !! 0)
unpackNum (List [n]) = unpackNum n
unpackNum notNum = throwError $ TypeMismatch "number" notNum
</code></pre>

<p>主函数 main 的实现中，通过 trapError 捕获了可能出现的错误</p>

<pre class="language-haskell"><code>main :: IO ()
main = do
  args &lt;- getArgs
  evaled &lt;- return $ liftM show $ readExpr (args !! 0) &gt;&gt;= eval
  putStrLn $ extractValue $ trapError evaled
</code></pre>

<p>这里对 apply 也做了扩展</p>

<pre class="language-haskell"><code>apply :: String -&gt; [LispVal] -&gt; ThrowsError LispVal
apply func args = maybe (throwError $ NotFunction "Unrecognized primitives function args" func) 
                  ($ args) 
                  (lookup func primitives)
</code></pre>

<p>这里利用 Either 进行异常和错误处理的方法值得关注。</p>

<h2 id="evalution-part-2">Evalution, Part 2</h2>

<h3 id="additional-primitives-partial-application">Additional Primitives: Partial Application</h3>

<p>类似 unpackNum ，现在我们也有了 unpackStr 和 unpack Num。 notString 和 notBool 的错误值匹配也是一个技巧。</p>

<pre class="language-haskell"><code>unpackStr :: LispVal -&gt; ThrowsError String
unpackStr (String s) = return s
unpackStr (Number s) = return $ show s
unpackStr (Bool s) = return $ show s
unpackStr notString = throwError $ TypeMismatch "string" notString

unpackBool :: LispVal -&gt; ThrowsError Bool
unpackBool (Bool b) = return b
unpackBool notBool = throwError $ TypeMismatch "boolean" notBool
</code></pre>

<p>二元逻辑判断的实现。</p>

<pre class="language-haskell"><code>boolBinop :: (LispVal -&gt; ThrowsError a) -&gt; (a -&gt; a -&gt; Bool) -&gt; [LispVal] -&gt; ThrowsError LispVal
boolBinop unpacker op args = if length args /= 2
                             then throwError $ NumArgs 2 args
                             else do left &lt;- unpacker $args !! 0
                                     right &lt;- unpacker $args !! 1
                                     return $ Bool $ left `op` right

numBoolBinop = boolBinop unpackNum
strBoolBinop = boolBinop unpackStr
boolBoolBinop = boolBinop unpackBool
</code></pre>

<p>书中给出的代码有些错误，现在完整的指令表是：</p>

<pre><code>primitives :: [(String, [LispVal] -&gt; ThrowsError LispVal)]
primitives = [("+", numericBinop (+)),
              ("-", numericBinop (-)),
              ("*", numericBinop (*)),
              ("/", numericBinop div),
              ("=", numBoolBinop (==)),
              ("&lt;", numBoolBinop (&lt;)),
              ("&gt;", numBoolBinop (&gt;)),
              ("/=", numBoolBinop (/=)),
              ("&gt;=", numBoolBinop (&gt;=)),
              ("&lt;=", numBoolBinop (&lt;=)),
              ("&amp;&amp;", boolBoolBinop (&amp;&amp;)),
              ("||", boolBoolBinop (||)),
              ("string=?", strBoolBinop (==)),
              ("string&gt;?", strBoolBinop (&gt;)),
              ("string&lt;?", strBoolBinop (&lt;)),
              ("string&lt;=?", strBoolBinop (&lt;=)),
              ("string&gt;=?", strBoolBinop (&gt;=)),
              ("mod", numericBinop mod),
              ("quotient", numericBinop quot),
              ("remainder", numericBinop rem),
              ("symbol?", unaryOp symbolp),
              ("string?", unaryOp stringp),
              ("number?", unaryOp numberp),
              ("bool?", unaryOp boolp),
              ("list?", unaryOp listp)]
</code></pre>

<h3 id="pattern-matching-2">Pattern Matching 2</h3>
<p>由于 if 语句的实现没有走 primitives ，而是直接取函数名，这里需要将它的 eval 放在前面：</p>

<pre class="language-haskell"><code>eval :: LispVal -&gt; ThrowsError LispVal
eval val@(String _) = return val
eval val@(Number _) = return val
eval val@(Bool _) = return val
eval (List [Atom "quote", val]) = return val
eval (List [Atom "if", pred, conseq, alt]) =
    do result &lt;- eval pred
       case result of
         Bool False -&gt; eval alt
         otherwise -&gt; eval conseq
eval (List (Atom func : args)) = mapM eval args &gt;&gt;= apply func
eval badForm = throwError $ BadSpecialForm "Unrecognized special form" badForm
</code></pre>

<h3 id="list-primitives-car-cdr-and-cons">List Primitives: car, cdr, and cons</h3>

<p>对于 car 来说，实现代码与匹配规则一一对应：</p>

<pre class="language-haskell"><code>(car (a b c)) = a
(car (a)) = a
(car (a b . c)) = a
(car a) = error (not a list)
(car a b) = error (car takes only one argument)
</code></pre>
<p>实现为：</p>

<pre class="language-haskell"><code>car :: [LispVal] -&gt; ThrowsError LispVal
car [List (x : xs)] = return x
car [DottedList (x : xs) _] = return x
car [badArg] = throwError $ TypeMismatch "pair" badArg
car badArgList = throwError $ NumArgs 1 badArgList
</code></pre>

<p>同理， cdr 规则</p>

<pre class="language-haskell"><code>(cdr (a b c)) = (b c)
(cdr (a b)) = (b)
(cdr (a)) = NIL
(cdr (a b . c)) = (b . c)
(cdr (a . b)) = b
(cdr a) = error (not list)
(cdr a b) = error (too many args)
</code></pre>

<p>实现为：</p>

<pre class="language-haskell"><code>cdr :: [LispVal] -&gt; ThrowError LispVal
cdr [List (x:xs)] = return $ List xs
cdr [DottedList (_ :xs) x] = return $ DottedList xs x
cdr [DottedList [xs] x] = return x
cdr [badArg] = throwError $ TypeMismatch "pari" badArg
cdr badArgList = throwError $ NumArgs 1 badArgList
</code></pre>

<p>同理添加 cons 和 eqv ，在 primitives 加入对应的 KV 对。</p>

<h3 id="equal-and-weak-typing-heterogenous-lists">Equal? and Weak Typing: Heterogenous Lists</h3>

<p>这里利用了 GHC 的扩展： Existential Types 。使用时需要调用 GHC 的 -fglasgow-exts 选项。</p>

<p>原文这两行似乎写反了。</p>

<pre class="language-haskell"><code>cdr [DottedList [xs] x] = return x
cdr [DottedList (_ :xs) x] = return $ DottedList xs x
</code></pre>

<p>这部分展示了一些使用扩展编译选项的类型推导技术，如 forall。</p>

<pre class="language-haskell"><code>data Unpacker = forall a . Eq a =&gt; AnyUnpacker (LispVal -&gt; ThrowsError a)
</code></pre>

<p>forall 的自动类型推导使得弱类型容器成为可能，进一步允许弱类型比较， 最终落实这个运算的是强和弱判等：</p>

<pre class="language-haskell"><code>eqv :: [LispVal] -&gt; ThrowsError LispVal
eqv [(Bool arg1), (Bool arg2)] = return $ Bool $ arg1 == arg2
eqv [(Number arg1), (Number arg2)] = return $ Bool $ arg1 == arg2
eqv [(String arg1), (String arg2)] = return $ Bool $ arg1 == arg2
eqv [(Atom arg1), (Atom arg2)] = return $ Bool $ arg1 == arg2
eqv [(DottedList xs x), (DottedList ys y)] = eqv [List $ xs ++ [x], List $ ys ++ [y]]
eqv [(List arg1), (List arg2)] = return $ Bool $ (length arg1 == length arg2) &amp;&amp;
                                 (and $ map eqvPair $ zip arg1 arg2)
                                 where eqvPair (x1, x2) = case eqv [x1, x2] of
                                                            Left err -&gt; False
                                                            Right (Bool val) -&gt; val
eqv [_, _] = return $ Bool False
eqv badArgList = throwError $ NumArgs 2 badArgList

equal :: [LispVal] -&gt; ThrowsError LispVal
equal [arg1, arg2] = do
  primitiveEqual &lt;- liftM or $ mapM (unpackEquals arg1 arg2)
                    [AnyUnpacker unpackNum, AnyUnpacker unpackStr, AnyUnpacker unpackBool]
  eqvEquals &lt;- eqv [arg1, arg2]
  return $ Bool $ (primitiveEqual || let (Bool x) = eqvEquals in x)

equal badArgList = throwError $ NumArgs 2 badArgList
</code></pre>

<h3 id="section-7">练习 6.4.1</h3>
<p>这个没太大技术含量，我自己就搞定了：</p>

<pre class="language-haskell"><code>eval (List [Atom "if", pred, conseq, alt]) =
    do result &lt;- eval pred
       case result of
         Bool True -&gt; eval conseq
         Bool False -&gt; eval alt
         otherwise -&gt; throwError $ TypeMismatch "bool" pred
</code></pre>

<h3 id="section-8">练习 6.4.2</h3>

<p>对 List 进行强和弱类型比较，这里比我想像的复杂，作者先实现了一个 List 比较函数。</p>

<pre class="language-haskell"><code>eqvList :: ([LispVal] -&gt; ThrowsError LispVal) -&gt; [LispVal] -&gt; ThrowsError LispVal
eqvList eqvFunc [(List arg1), (List arg2)] = return $ Bool $ (length arg1 == length arg2) &amp;&amp;
                                             (all eqvPair $ zip arg1 arg2)
    where eqvPair (x1, x2) = case eqvFunc [x1, x2] of
                               Left err -&gt; False
                               Right (Bool val) -&gt; val
</code></pre>

<p>然后用它重写了 eqv 和 equal 的 List 实现：</p>

<pre class="language-haskell"><code>eqv [l1@(List arg1), l2@(List arg2)] = eqvList eqv [l1, l2]
eqv [(DottedList xs x), (DottedList ys y)] = eqv [List $ xs ++ [x], List $ ys ++ [y]]
...
equal [l1@(List arg1), l2@(List arg2)] = eqvList equal [l1, l2]
equal [(DottedList xs x), (DottedList ys y)] = equal [List $ xs++[x], List $ ys++[y]]
</code></pre>

<p>因为haskell的函数模式匹配是按照书写顺序尝试的，所以要注意 函数的顺序。</p>

<h3 id="section-9">练习 6.4.3</h3>

<p>作者给出了两种不同的 cond 实现，这里我采用了第一种：</p>

<pre class="language-haskell"><code>eval (List ((Atom "cond"):cs)) = do
  b &lt;- (liftM (take 1 . dropWhile f) $ mapM condClause cs) &gt;&gt;= cdr
  car [b] &gt;&gt;= eval
      where condClause (List [Atom "else", b]) = return $ List [Bool True, b]
            condClause (List [p,b]) = do q &lt;- eval p
                                         case q of
                                           Bool _ -&gt; return $ List [q,b]
                                           _ -&gt; throwError $ TypeMismatch "bool" q
            condClause v = throwError $ TypeMismatch "(pred body)" v
            f = \(List [p,b]) -&gt; case p of
                                   (Bool False) -&gt; True
                                   _ -&gt; False
</code></pre>

<p>根据这个代码，我做出了 case 的实现：</p>

<pre class="language-haskell"><code>eval (List ((Atom "case"):cs)) = do
  b &lt;- (liftM (take 1 . dropWhile f ) $ mapM condClause (tail cs)) &gt;&gt;= cdr
  car [b] &gt;&gt;= eval
      where cond = cs!!0
            condClause (List [Atom "else", b]) = return $ List [Bool True, b]
            condClause (List [p,b]) = do x &lt;- eval cond
                                         y &lt;- eval p
                                         q &lt;- eqv [x, y]
                                         case q of
                                           Bool _ -&gt; return $ List [q, b]
                                           _ -&gt; throwError $ TypeMismatch "bool" q
            condClause v = throwError $ TypeMismatch "(pred body)" v
            f = \(List [p,b]) -&gt; case p of
                                   (Bool False) -&gt; True
                                   _ -&gt; False
</code></pre>

<p>至此以后的章节，都没有课后习题了。</p>

<h2 id="building-a-repl-basic-io">Building a REPL: Basic I/O</h2>

<p>书中使用了 Parsec 模块的 try ，所以这里不导入 IO 的 try。</p>

<pre class="language-haskell"><code>import IO hiding (try)
IO Monad 本身已经输出状态了，所以这里用 Monad Then 传递：

flushStr :: String -&gt; IO ()
flushStr str = putStr str &gt;&gt; hFlush stdout
</code></pre>

<p>这个 IO Monad then 的运用非常漂亮</p>

<pre class="language-haskell"><code>readPrompt :: String -&gt; IO String
readPrompt prompt = flushStr prompt &gt;&gt; getLine
</code></pre>

<p>摘录： That’s why we write their types using the type variable “m”, and include the type constraint “Monad m =&gt;”</p>

<pre class="language-haskell"><code>until_ :: Monad m =&gt; (a -&gt; Bool) -&gt; m a -&gt; (a -&gt; m ()) -&gt; m ()
until_ pred prompt action = do 
  result &lt;- prompt
  if pred result 
     then return ()
     else action result &gt;&gt; until_ pred prompt action
</code></pre>

<h2 id="adding-variables-and-assignment-mutable-state-in-haskell">Adding Variables and Assignment: Mutable State in Haskell</h2>

<p>State Monad 对于简单的有状态应用非常好用。</p>

<p>这里使用 state threads，具体而言是 Data.IORef。</p>

<pre class="language-haskell"><code>import Data.IORef

type Env = IORef [(String, IORef LispVal)]
建立空环境的 nullEnv 函数：

nullEnv :: IO Env
nullEnv = newIORef []
</code></pre>

<p>将异常 lift 为自定义的 IOThrowsError</p>

<pre class="language-haskell"><code>liftThrows :: ThrowsError a -&gt; IOThrowsError a
liftThrows (Left err) = throwError err
liftThrows (Right val) = return val
</code></pre>

<p>原文摘录：Methods in typeclasses resolve based on the type of the expression, so throwError and return (members of MonadError and Monad, respectively) take on their IOThrowsError definitions.</p>

<p>函数 runIOThrows 对 trapError 进行了 IO 包装：</p>

<pre class="language-haskell"><code>runIOThrows :: IOThrowsError String -&gt; IO String
runIOThrows action = runErrorT (trapError action) &gt;&gt;= return . extractValue
</code></pre>

<p>函数 lookup 对键值对序列做 key 查询，返回 Maybe value。所以这里利用 const True 做了一个 isBound 查询函数。</p>

<pre class="language-haskell"><code>isBound :: Env -&gt; String -&gt; IO Bool
isBound envRef var = readIORef envRef &gt;&gt;= return . maybe False (const True) . lookup var
</code></pre>

<p>对 IORef 环境的经典读写操作：</p>

<pre class="language-haskell"><code>getVar :: Env -&gt; String -&gt; IOThrowsError LispVal
getVar envRef var = do env &lt;- liftIO $ readIORef envRef
                       maybe (throwError $ UnboundVar "Getting an unbound variable" var)
                             (liftIO . readIORef) (lookup var env)

setVar :: Env -&gt; String -&gt; LispVal -&gt; IOThrowsError LispVal
setVar envRef var value = do env &lt;- liftIO $ readIO envRef
                             maybe (throwError $ UnboundVar "Setting an unboud variable" var)
                                   (liftIO . (flip writeIORef value))
                                   (lookup var env)
                             return value
</code></pre>

<p>在 bindVars 函数中，利用了 Monadic 管道操作技巧：</p>

<pre class="language-haskell"><code>bindVars :: Env -&gt; [(String, LispVal)] -&gt; IO Env
bindVars envRef bindings = readIORef envRef &gt;&gt;= extendEnv bindings &gt;&gt;= newIORef
    where extendEnv bindings env = liftM (++ env) (mapM addBinding bindings)
          addBinding (var, value) = do ref &lt;- newIORef value
                                       return (var, ref)
</code></pre>

<p>eval 中传入 env 参数以保持状态，其中 cond 和 case 基本上算重写了一遍……</p>

<pre class="language-haskell"><code>eval :: Env -&gt; LispVal -&gt; IOThrowsError LispVal
eval env val@(String _) = return val
eval env val@(Number _) = return val
eval env val@(Bool _) = return val
eval env (Atom id) = getVar env id
eval env (List [Atom "quote", val]) = return val
eval env (List [Atom "if", pred, conseq, alt]) =
    do result &lt;- eval env pred
       case result of
         Bool True -&gt; eval env conseq
         Bool False -&gt; eval env alt
         otherwise -&gt; throwError $ TypeMismatch "bool" pred
eval env (List ((Atom "cond"):cond:cs)) = do
  result &lt;- condClause env cond
  if (f result) 
     then eval' env result
     else eval env (List ((Atom "cond"):cs))
    where condClause env (List [Atom "else", b]) = return $ List [Bool True, b]
          condClause env (List [p,b]) = do q &lt;- eval env p
                                           case q of
                                             Bool _ -&gt; return $ List [q,b]
                                             _ -&gt; throwError $ TypeMismatch "bool" q
          condClause env v = throwError $ TypeMismatch "(pred body)" v
          f = \(List [p,b]) -&gt; case p of
                                 (Bool True) -&gt;  True
                                 (Bool False) -&gt; False
          eval' = \env (List [p, b]) -&gt; eval env b                                      
eval env (List ((Atom "case"):c:cond:cs)) = do
  x &lt;- eval env c
  result &lt;- condClause env cond x
  if (f result)
     then eval' env result
     else eval env (List ((Atom "case"):x:cs))
    where condClause env (List [Atom "else", b]) x = return $ List [Bool True, b]
          condClause env (List [p,b]) x = do y &lt;- eval env p
                                             q &lt;- return $ eqv [x, y]
                                             (\e -&gt; let v = extractValue e
                                                    in case v of
                                                         Bool _ -&gt; return $List [v, b]
                                                         _ -&gt; throwError $ TypeMismatch "bool" v) q
          condClause env v x = throwError $ TypeMismatch "(pred body)" v
          f = \(List [p,b]) -&gt; case p of
                                 (Bool False) -&gt; False
                                 _ -&gt; True
          eval' = \env (List [p, b]) -&gt; eval env b
eval env (List [Atom "set!", Atom var, form]) =
    eval env form &gt;&gt;= setVar env var
eval env (List [Atom "define", Atom var, form]) =
    eval env form &gt;&gt;= defineVar env var
eval env (List (Atom func : args)) = mapM (eval env) args &gt;&gt;= liftThrows.apply func
eval env badForm = throwError $ BadSpecialForm "Unrecognized special form" badForm
</code></pre>

<p>因为要保持环境，单行语句和 REPL 的执行函数分成了两个：</p>

<pre class="language-haskell"><code>runOne :: String -&gt; IO ()
runOne expr = nullEnv &gt;&gt;= flip evalAndPrint expr

runRepl :: IO ()
runRepl = nullEnv &gt;&gt;= until_ (== "quit") (readPrompt "Lisp&gt;&gt;&gt; ") . evalAndPrint

main :: IO ()
main = do args &lt;- getArgs
          case length args of
              0 -&gt; runRepl
              1 -&gt; runOne $ args !! 0
              otherwise -&gt; putStrLn "Program takes only 0 or 1 argument"
</code></pre>

<h2 id="defining-scheme-functions-closures-and-environments">Defining Scheme Functions: Closures and Environments</h2>

<p>代码示例基本没什么错误
函数的定义和函数执行是两个相关的内容。</p>

<p>扩展后的 LispVal：</p>

<pre class="language-haskell"><code>data LispVal = Atom String
             | List [LispVal]
             | DottedList [LispVal] LispVal
             | Number Integer
             | Float Double
             | String String
             | Bool Bool
             | Character Char
             | Ratio Rational
             | Complex (Complex Double)
             | Vector (Array Int LispVal)
             | PrimitiveFunc ([LispVal] -&gt; ThrowsError LispVal)
             | Func {params :: [String], vararg :: (Maybe String),
                     body :: [LispVal], closure :: Env}
             | Nil ()
</code></pre>

<p>函数定义涉及参数名、参数列、函数体和闭包。新的 apply 实现了函数定义和执行：</p>

<p>函数解析代码中先判定了形参与实参是否一致（如果没有指定动态参数， 给出的实参个数又与形参不同，则报错）。然后利用 curry 方法生成 该函数的函数实现。在各操作（绑定作用域、参数和解析函数体）的过程 中，以 Monad bind 串起各部分。注意这里 bindVars 接受的第一 个函数是 closure ，在 eval 中我们可以看到 closure 是 外部的 env。在传值过程中，env 复制为函数 closure。</p>

<pre class="language-haskell"><code>apply :: LispVal -&gt; [LispVal] -&gt; IOThrowsError LispVal
apply (PrimitiveFunc func) args = liftThrows $ func args
apply (Func params varargs body closure) args =
    if num params /= num args &amp;&amp; varargs == Nothing
       then throwError $ NumArgs (num params) args
       else (liftIO $ bindVars closure $ zip params args) &gt;&gt;= bindVarArgs varargs &gt;&gt;= evalBody
    where remainingArgs = drop (length params) args
          num = toInteger . length
          evalBody env  = liftM last $ mapM (eval env) body
          bindVarArgs arg env = case arg of
                                  Just argName -&gt; liftIO $ bindVars env [(argName, List $ remainingArgs)]
                                  Nothing -&gt; return env
</code></pre>

<p>函数相关的 eval 实现。</p>

<pre class="language-haskell"><code>eval env (List (Atom "define" : List (Atom var : params) : body)) =
    makeNormalFunc env params body &gt;&gt;= defineVar env var
eval env (List (Atom "define" : DottedList (Atom var : params) varargs : body)) =
    makeVarargs varargs env params body &gt;&gt;= defineVar env var
eval env (List (Atom "lambda" : List params : body)) =
    makeNormalFunc env params body
eval env (List (Atom "lambda" : DottedList params varargs : body)) =
    makeVarargs varargs env params body
eval env (List (Atom "lambda" : varargs@(Atom _) : body)) =
    makeVarargs varargs env [] body
eval env (List (function : args)) = do func &lt;- eval env function
                                       argVals &lt;- mapM (eval env) args
                                       apply func argVals
</code></pre>

<p>在 eval 中使用到的函数构造代码：</p>

<pre class="language-haskell"><code>makeFunc varargs env params body = return $ Func (map showVal params) varargs body env
makeNormalFunc = makeFunc Nothing
makeVarargs = makeFunc . Just . showVal
</code></pre>

<p>环境初始化代码进行了进一步的扩展，将所有的 primitive 封装为函数。这部分实现的简洁有力：</p>

<pre class="language-haskell"><code>primitiveBindings :: IO Env
primitiveBindings = nullEnv &gt;&gt;= (flip bindVars $ map makePrimitiveFunc primitives)
    where makePrimitiveFunc (var, func) = (var, PrimitiveFunc func)

runOne :: String -&gt; IO ()
runOne expr = primitiveBindings &gt;&gt;= flip evalAndPrint expr

runRepl :: IO ()
runRepl = primitiveBindings &gt;&gt;= until_ (== "quit") (readPrompt "Lisp&gt;&gt;&gt; ") . evalAndPrint
</code></pre>

<p>最后这几章的内容中，数次出现了 flip 操作。</p>

<h2 id="creating-io-primitives-file-io">Creating IO Primitives: File I/O</h2>

<p>这一章代码没什么争议
因为输入的参数不同（数组和数值的区别），io 的指令集是独立实现的：</p>

<pre class="language-haskell"><code>ioPrimitives :: [(String, [LispVal] -&gt; IOThrowsError LispVal)]
ioPrimitives = [("apply", applyProc),
                ("open-input-file", makePort ReadMode),
                ("open-output-file", makePort WriteMode),
                ("close-input-port", closePort),
                ("close-output-port", closePort),
                ("read", readProc),
                ("write", writeProc),
                ("read-contents", readContents),
                ("read-all", readAll)]
</code></pre>

<p>其底层的文件操作都是对 haskell 库的浅封装：</p>

<pre class="language-haskell"><code>applyProc :: [LispVal] -&gt; IOThrowsError LispVal
applyProc [func, List args] = apply func args
applyProc (func : args) = apply func args

readProc :: [LispVal] -&gt; IOThrowsError LispVal
readProc [] = readProc [Port stdin]
readProc [Port port] = (liftIO $ hGetLine port) &gt;&gt;= liftThrows . readExpr

writeProc :: [LispVal] -&gt; IOThrowsError LispVal
writeProc [obj] = writeProc [obj, Port stdout]
writeProc [obj, Port port] = liftIO $ hPrint port obj &gt;&gt; (return $ Bool True)

readContents :: [LispVal] -&gt; IOThrowsError LispVal
readContents [String filename] = liftM String $ liftIO $ readFile filename

makePort :: IOMode -&gt; [LispVal] -&gt; IOThrowsError LispVal
makePort mode [String filename] = liftM Port $ liftIO $ openFile filename mode

closePort :: [LispVal] -&gt; IOThrowsError LispVal
closePort [Port port] = liftIO $ hClose port &gt;&gt; (return $ Bool True)
closePort _ = return $ Bool False

load :: String -&gt; IOThrowsError [LispVal]
load filename = (liftIO $ readFile filename) &gt;&gt;= liftThrows . readExprList

readAll :: [LispVal] -&gt; IOThrowsError LispVal
readAll [String filename] = liftM List $ load filename
</code></pre>

<p>为了处理单语句和多语句，代码解析也进行了抽象，分别实现具体的操作</p>

<pre class="language-haskell"><code>readOrThrow :: Parser a -&gt; String -&gt; ThrowsError a
readOrThrow parser input = case parse parser "lisp" input of
                             Left err -&gt; throwError $ Parser err
                             Right val -&gt; return val

readExpr = readOrThrow parseExpr
readExprList = readOrThrow (endBy parseExpr spaces)
</code></pre>

<p>环境初始化，引入指令集的实现，针对 IO 作了扩展：</p>

<pre class="language-haskell"><code>primitiveBindings :: IO Env
primitiveBindings = nullEnv &gt;&gt;= (flip bindVars $ map (makeFunc IOFunc) ioPrimitives
                                        ++ map (makeFunc PrimitiveFunc) primitives)
    where makeFunc constructor (var, func) = (var, constructor func)
</code></pre>

<p>交互过程做了相应的调整</p>

<pre class="language-haskell"><code>runOne :: [String] -&gt; IO ()
runOne args = do
  env &lt;- primitiveBindings &gt;&gt;= flip bindVars [("args", List $ map String $ drop 1 args)]
  (runIOThrows $ liftM show $ eval env (List [Atom "load", String (args !! 0)]))
              &gt;&gt;= hPutStrLn stderr

runRepl :: IO ()
runRepl = primitiveBindings &gt;&gt;= until_ (== "quit") (readPrompt "Lisp&gt;&gt;&gt; ") . evalAndPrint

main :: IO ()
main = do args &lt;- getArgs
          if null args then runRepl else runOne $ args
</code></pre>

<h2 id="towards-a-standard-library-fold-and-unfold">Towards a Standard Library: Fold and Unfold</h2>

<p>这一章只有 scheme 的代码和内置库实现，留待想要学习 sheme 语言的时候深入。</p>

<h2 id="conclusion--further-resources">Conclusion &amp; Further Resources</h2>

<p>附录跳过</p>

<h2 id="section-10">总结</h2>

<p><a href="http://jonathan.tang.name/files/scheme_in_48/tutorial/overview.html">原书在线阅读</a></p>

<p><a href="http://en.wikibooks.org/wiki/Write_Yourself_a_Scheme_in_48_Hours/Answers">参考答案</a></p>

<p>时间和精力有限，我没有认真的去做每一道题，而仅仅是把在线答案中的每一个 调试验证了一遍。示例中还是有一些小错误的，如果只是复制，无法顺利编译运 行，有一些我在笔记中有提到，附件中是我自己的练习代码。</p>

<p>从我个人体验来讲，用 ghci 来练习，要比编译后调试要快捷得多。不过从第 六章 Evalution 2 开始，需要 -XExistentialQuantification 选项。 如果你和我一样使用 emacs 的 ghci 集成 shell ，没办法用 ghci -XExistentialQuantification 形式启动交互环境，可以在 ghci 中执行 :set -XExistentialQuantification 加载这一选项。</p>

<p>这本书不应该作为 Haskell 学习的第一本书，甚至也不一定应该是第二本书。 学习此书最好有初步的 lisp/scheme 知识。</p>

<p>我的阅读过程还是比较粗糙的，建议在遇到不能完全理解的地方，用 ghci 调试 一下。</p>

<p>作为第一本书，<a href="http://www.cs.utah.edu/~hal/docs/daume02yaht.pdf">Yet Another Haskell Tutorial</a> 更系统，内容循序渐进。</p>

<p>作为第二本书，<a href="http://www.realworldhaskell.org/">Real World Haskell</a> 更有实践性，曲线比较平滑。</p>

<p>本书应该作为从初阶到中阶的一本阶段总结辅导。或者，除非你有 scheme/lisp 基础，对第一本书也学的比较顺利……</p>


    <hr>
    <div class="pagination">
      <ul>
      
        <li class="prev"><a href="/others/2013/02/12/about" title="About">&larr; Previous</a></li>
      
        <li><a href="/archive.html">Archive</a></li>
      
        <li class="next"><a href="/tech/2013/02/17/how-to-create-a-http-put-use-python" title="How To Create a HTTP PUT use Python">Next &rarr;</a></li>
      
      </ul>
    </div>
    <hr>
    


  <div id="disqus_thread"></div>
<script type="text/javascript">
    var disqus_developer = 1;
    var disqus_shortname = 'jekyllbootstrap'; // required: replace example with your forum shortname
    
    /* * * DON'T EDIT BELOW THIS LINE * * */
    (function() {
        var dsq = document.createElement('script'); dsq.type = 'text/javascript'; dsq.async = true;
        dsq.src = 'http://' + disqus_shortname + '.disqus.com/embed.js';
        (document.getElementsByTagName('head')[0] || document.getElementsByTagName('body')[0]).appendChild(dsq);
    })();
</script>
<noscript>Please enable JavaScript to view the <a href="http://disqus.com/?ref_noscript">comments powered by Disqus.</a></noscript>
<a href="http://disqus.com" class="dsq-brlink">blog comments powered by <span class="logo-disqus">Disqus</span></a>




  </div>
  
  <div class="span4">
    <h4>Published</h4>
    <div class="date"><span>13 February 2013</span></div>

  
    <h4>Tags</h4>
    <ul class="tag_box">
    
    


  
     
    	<li><a href="/tags.html#learn-ref">learn <span>1</span></a></li>
     
    	<li><a href="/tags.html#notes-ref">notes <span>1</span></a></li>
     
    	<li><a href="/tags.html#haskell-ref">haskell <span>1</span></a></li>
    
  



    </ul>
    
  </div>
</div>


      </div>

      <footer>
        <p>&copy; 刘鑫 2013
          with help from <a href="http://jekyllbootstrap.com" target="_blank" title="The Definitive Jekyll Blogging Framework">Jekyll Bootstrap</a>
          and <a href="http://twitter.github.com/bootstrap/" target="_blank">Twitter Bootstrap</a>
        </p>
      </footer>

    </div> <!-- /container -->

    
  </body>
</html>

